//EVIL DINAH
class EvilDinah : DDOrigBoss
{
	default
	{
		//$Category RPMonsters
		Health 500; //2000;
		Radius 12; // 20;
		Height 48; // 64;
		Speed 10;
		XScale 1.0; //0.8572;
		YScale 0.95; //0.8572;
		PainChance 128;
		Painchance "Beats", 16;
		Painchance "Fire", 64;
		DamageFactor "Suck", 0;
		DamageFactor "Cricket", 2;
		MeleeRange 64;
		MeleeThreshold 128;
		MONSTER;
		+FLOORCLIP;
		+NOINFIGHTING;
		+NOBLOOD;
		+MISSILEMORE;
		+DONTHARMCLASS;
		SeeSound "dinah/giggle";
		PainSound "evildinah/pain";
		DeathSound "virtuoso/death";
		ActiveSound "dinah/giggle";
		Obituary "%o couldn't stop hitting herself.";
		Tag "Dinah Wyck...?";
	}
	
	void A_UseRandomWeapon()
	{
		state ATKState;
		int atktype = random(1,8);
		if (atktype == 1) ATKState = FindState("CricketAttk");
		if (atktype == 2) ATKState = FindState("BlunderAttk");
		if (atktype == 3) ATKState = FindState("JackAttk");
		if (atktype == 4) ATKState = FindState("EggAttk");
		if (atktype == 5) ATKState = FindState("PikeAttk");
		if (atktype == 6) ATKState = FindState("MusicAttk");
		if (atktype == 7) ATKState = FindState("SuckAttk");
		if (atktype == 8) ATKState = FindState("CandleAttk");
		if (ATKState != null) SetState(ATKState);
	}

	void A_CheckPlayerWeapon()
	{
		actor player = players[0].mo;
		let MiscItem = PlayerStatItem(Player.FindInventory("PlayerStatItem"));
		let PlayerWeapon = Player.Player.ReadyWeapon;
		if (random(1,256) <= 32)
		{
			state ATKState;
			if (PlayerWeapon is "CricketBat") ATKState = FindState("CricketAttk");
			if (PlayerWeapon is "BlunderAxe") ATKState = FindState("BlunderAttk");
			if (PlayerWeapon is "AppleJack") ATKState = FindState("JackAttk");
			if (PlayerWeapon is "Scrambler") ATKState = FindState("EggAttk");
			if (PlayerWeapon is "WizardPike") ATKState = FindState("PikeAttk");
			if (PlayerWeapon is "Grimophone") ATKState = FindState("MusicAttk");
			if (PlayerWeapon is "SuperVac") ATKState = FindState("SuckAttk");
			if (PlayerWeapon is "FosnianCandle") ATKState = FindState("CandleAttk");
			if (ATKState != null) SetState(ATKState);
		}
	}
	
	States
	{
		Spawn:
			EDIN A 7 A_Look();
			EDIN B 5 A_Look();
			EDIN C 7 A_Look();
			EDIN D 5 A_Look();
			Loop;
		Inactive:
			EDIN A 7;
			EDIN B 5;
			EDIN C 7;
			EDIN D 5;
			Loop;
		Active:
			EDIN A 0;
			Goto Spawn;
		See:
			DINA BBCCDDEEBBCCDDEE 2 A_Chase();
			DINA BCDE 4 A_FastChase;
			DINA B 0 A_Jump(32,"Missile");
			DINA E 0 A_DynamicMusic();
			Loop;
		Missile:
			DINA F 0 A_DynamicMusic();
			DINA F 0 A_FaceTarget();
			DINA F 0 
			{
				if (random(1,256) <= 32) A_UseRandomWeapon();
				else A_CheckPlayerWeapon();
			}
			DINA F 8 A_StartSound("dinah/giggle",CHAN_VOICE);
			DINA G 8 A_SpawnProjectile("SpinnerShotEvil",40);
			DINA A 4;
			Goto See;
		CricketAttk:
			DINA F 4 A_StartSound("malletswing",CHAN_WEAPON);
			DINA G 8 
			{
				A_StartSound("croquet/ballfire",CHAN_WEAPON);
				if (random(1,2) == 1) A_FatAttack1("RealBadCroquetBall");
				else A_FatAttack2("RealBadCroquetBall");
			}
			DINA A 4 A_FastChase();
			Goto See;
		BlunderAttk:
			DWBL A 6;
			DWBL B 9 
			{
				A_StartSound("blunderfire2",CHAN_WEAPON);
				A_SpawnProjectile("BadBlundershot",48,0,random(-5,5),0,0);
				A_SpawnProjectile("BadBlundershot",48,0,random(-7,7),0,0);
				A_SpawnProjectile("BadBlundershot",48,0,random(-11,11),0,0);
			}
			DWBL A 4;
			Goto See;
		JackAttk:
			DWAJ A 4 A_StartSound("malletswing",CHAN_WEAPON);
			DWAJ A 0 A_Jump(128,3);
			DWAJ A 8 A_FatAttack1("Jackshot");
			DWAJ A 0 A_Jump(256,2);
			DWAJ B 8 A_FatAttack2("Jackshot");
			DWAJ A 4;
			Goto See;
		EggAttk:
			DWEG A 8 A_StartSound("misc/p_pkup",CHAN_VOICE);
			DWEG A 4 A_Facetarget();
			DWEG B 8 
			{
				A_StartSound("PanClang",CHAN_WEAPON);
				A_Throwgrenade("Eggshot",2,24,random(6,12),0);
			}
			DWEG A 4;
			Goto See;
		PikeAttk:
			DWPK B 0 A_StartSound("Handmother/Zap",CHAN_WEAPON);
			DWPK BB 3 bright;
			DWPK B 3 bright A_SpawnProjectile("miniZapshot",40);
			DWPK B 3 bright;
			DWPK B 0 bright A_SpawnProjectile("miniZapshot",40,-48);
			DWPK B 3 bright A_SpawnProjectile("miniZapshot",40,48);
			DWPK B 3 bright;
			DWPK B 3 bright A_SpawnProjectile("Zapshot",40);
			DWPK AA 3 bright;
			Goto See;
		MusicAttk:
			DWGP A 8 A_StartSound("misc/p_pkup",CHAN_VOICE);
			DWGP A 5 A_SpawnProjectile("badrecordshot",40,0,45);
			DWGP A 5;
			DWGP A 5 A_SpawnProjectile("badrecordshot",40,0,0);
			DWGP A 5;
			DWGP A 5 A_SpawnProjectile("badrecordshot",40,0,-45);
			DWGP A 5 A_CPosRefire();
			Goto MusicAttk+1;
		SuckAttk:
			DWSV A 8 A_StartSound("misc/p_pkup",CHAN_VOICE);
			DWSV B 12 A_SpawnProjectile("badvacshot",40,0);
			DWSV A 7;
			Goto See;
		CandleAttk:
			DWFC A 10 A_StartSound("dinah/giggle",CHAN_VOICE);
			DWBL BBBB 3 
			{
				A_StartSound("weapons/fcandlefire",CHAN_WEAPON);
				A_SpawnProjectile("badcandleshot",40,0,random(-5,5));
			}
			DINA A 2 A_CPosRefire();
			Goto CandleAttk+1;
		Melee:
			DINA F 0 A_DynamicMusic();
			DINA F 2 A_Facetarget();
			DINA FF 2 A_Recoil(-2);
			DINA G 0 ThrustThingz (0, 27, 0, 0);
			DINA F 2 ThrustThing (angle*256/360, 18, 0, 0);
			DINA G 2;
			DINA G 4 A_CustomMeleeAttack (random(25,40), "mallethit", "malletswing", "", 1);
			Goto See;
		Pain:
			DINA H 2 
			{
				A_DynamicMusic();
				A_FaceTarget();
			}
			DINA H 6 
			{
				ThrustThingZ (0, 24, 0, 0);
				ThrustThing (angle*256/360+180, 6, 0, 0);
				A_Pain();
			}
			DINA A 6 A_Jump(128,"Missile");
			Goto See;
		Death:
			DINA HHHHHHHH 4 Bright
			{
				A_StartSound("weapons/cherryboom",CHAN_WEAPON);
				A_SpawnItemEX("BossBoom",random(20,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION);
			}
			DINA H 3 A_FaceTarget();
			DINA H 5 
			{
				ThrustThingZ(0, 24, 0, 0);
				ThrustThing(angle*256/360+180, 6, 0, 0);
				A_Scream();
			}
			DINA H 2 bright A_NoBlocking();
			DANS RSTUVWXYZ 2 bright;
			Stop;
		}
}

class SpinnerShotEvil : EEProjectile
{
	default
	{
		Radius 8;
		Height 6;
		Speed 14;
		DamageFunction (finaldamagedealt);
		DamageType "Spinner";
		+SEEKERMISSILE
		//+NOEXTREMEDEATH
		Projectile;
		renderstyle "Add";
		SeeSound "weapons/spinnerfire";
		DeathSound "";
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		finaldamagedealt = (5 * random(1,8));
	}

	States
	{
		Spawn:
			SPNR A 0 Bright NoDelay
			{
				A_SpawnItemEx("Firetrail", 0, 0, 0, 0, 0, 0, 180, 128);
				A_AlertMonsters();
				A_StartSound("weapons/cherrywick",CHAN_7,CHANF_LOOPING);
			}
			SPNR AABB 1 Bright 
			{
				A_SeekerMissile(12,12,SMF_LOOK|SMF_PRECISE);
			}
			Loop;
		Death:
			SPNR C 0 
			{
				A_StopSound(CHAN_7);
				A_AlertMonsters();
			}
			FSPE A 2 Bright 
			{
				A_Explode(128,XF_HURTSOURCE,true,32);
				A_StartSound("weapons/cherryboom",CHAN_7);
				A_Quake(1, 4, 0, 512, "nosound/silent");
				A_Quake(4, 4, 0, 256, "nosound/silent");
			}
			FSPE BCDE 2 Bright;
			Stop;
	}
}

class BadBlunderSmokeSmallF : BlunderSmokeSmallF
{
	default
	{
		Speed 10;
	}
}

class BadBlunderSmokeSmall : BlunderSmokeSmall
{
	default
	{
		Speed 5;
	}
}

class RealBadCroquetBall : croquetball
{
	default
	{
		Speed 40;
	}
}

class Realbadjackshot : jackshot
{
	default
	{
		Damage 5;
	}
}

class BadCandleShot : CandleShot
{
	default
	{
		Speed 60;
		Damage 6;
		SeeSound "weapons/fcandlefire";
	}
}

class badrecordshot : recordshot
{
	default
	{
		Speed 20;
		Damage 3;
	}
	
	States
	{
		Spawn:
			RBUL ABAB 1;
			RBUL AB 1 A_SeekerMissile (12,12,SMF_LOOK);
			Loop;
		Death:
			BASH ABC 4 Bright;
			BASH DE 3 bright;
			Stop;
	}
}

class badvacshot : vacshot
{
	default
	{
		speed 20;
	}
}
