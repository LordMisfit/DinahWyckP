// DECORATE FILE IS CURRENTLY EMPTY NOW :V
// This file will remain in case new DECORATE code to be converted has to be hooked up to the mod. >.>
//FRACTAL CHICKEN
actor FractalChicken : EECreature 11824
{
	//$Category RPMonsters
	Health 100
	Radius 20
	Height 40
	Mass 1000
	Speed 20
	PainChance 256
	DamageFactor "Lead", 5
	MONSTER
	+FLOORCLIP
	+NOINFIGHTING
	+NOBLOOD
	+DONTHARMCLASS
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound "chook/sight"
	ActiveSound "chook/sight"
	PainSound "chook/pain"
	DeathSound "aqua/kill1"
	Obituary "%o chooked out thanks to a Fractal Chicken."
	Tag "Fractal Chicken"
	DropItem "Eggammo", 96
	States
	{
		Spawn:
			CHKS ABCB 6 A_Look
			Loop
		See:
			CHKS A 5
			CHKS DDEE 3 A_Chase
			CHKS FC 3
			Loop
		Missile:
			CHKS G 0 A_DynamicMusic
			CHKS H 0 A_Playsound("chook/sight",1)
			CHKS GHI 5 A_FaceTarget
			CHKS J 12 A_Throwgrenade("EvilEggGrenade",2,random(8,12),random(6,12),0)
			CHKS IHG 3
			Goto See
		Pain:
			CHKS F 6 A_Pain
			CHKS CB 3 
			Goto See
		Death.Suck:
			CHKS F 5 A_PlaySound("VacDed",CHAN_BODY)
			CHKS F 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,4)
			Stop
		Death:
			CHKS F 3 A_Scream
			DANS R 1 bright A_ThrowGrenade("ChookGrenade",2,random(2,6),random(6,10),0)
			DANS R 1 bright A_ThrowGrenade("ChookGrenade",2,random(2,6),random(6,10),0)
			DANS R 1 bright A_ThrowGrenade("ChookGrenade",2,random(2,6),random(6,10),0)
			DANS S 3 bright A_NoBlocking
			DANS TUVWXYZ 2 bright
			Stop
	}
}

ACTOR ChookGrenade : EEProjectile
{
	Radius 3
	Height 3
	Speed 10
	Damage 1
	Scale 0.25
	PROJECTILE
	-NOGRAVITY
	RENDERSTYLE Normal
	States
	{
		Spawn:
			CHKS D 3
			loop
		Death:
			CHKS DEDE 2
			CHKS E 0 A_SpawnItemEX ("FractalChick",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

actor FractalChick : EECreature 11825
{
	//$Category RPMonsters
	Health 30
	Radius 16
	Height 32
	Mass 1000
	Speed 20
	Scale 0.5
	PainChance 256
	DamageFactor "Lead", 5
	MONSTER
	+FLOORCLIP
	+NOINFIGHTING
	+NOBLOOD
	+DONTHARMCLASS
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound "chook/sight"
	ActiveSound "chook/sight"
	PainSound "chook/pain"
	DeathSound "aqua/kill1"
	Obituary "%o chooked out thanks to a Fractal Chick."
	Tag "Fractal Chick"
	States
	{
		Spawn:
			CHKS ABCB 6 A_Look
			Loop
		See:
			CHKS A 3
			CHKS DDEE 2 A_Chase
			CHKS FC 2
			Loop
		Missile:
			CHKS G 0 A_DynamicMusic
			CHKS G 0 A_Jump(32,"Grow")
			CHKS G 0 A_Playsound("chook/sight",1)
			CHKS GHI 5 A_FaceTarget
			CHKS J 12 A_Throwgrenade("EvilEggMini",1,random(12,14),random(6,10),0)
			CHKS IHG 3
			Goto See
		Grow:
			CHKS G 0 A_DynamicMusic
			CHKS H 0 A_Playsound("chook/sight",1)
			CHKS ABCB 6
			CHKS ABCBABCB 4
			CHKS ABCBABCB 2
			CHKS A 0 A_Die("growingpains")
			Goto See
		Pain:
			CHKS F 6 A_Pain
			CHKS CB 3 
			Goto See
		Death.Suck:
			CHKS F 5 A_PlaySound("VacDed",CHAN_BODY)
			CHKS F 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,1)
			Stop
		Death.Growingpains:
			DANS S 2 bright A_SpawnItemEx("FractalChicken",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			DANS TUVWXYZ 2 bright
			Stop
		Death:
			CHKS F 3 A_Scream
			DANS R 3 bright 
			DANS S 3 bright A_NoBlocking
			DANS TUVWXYZ 2 bright
			Stop
	}
}

//FRACTAL Hen Boss
actor FractalHen : EECreature 11826
{
	//$Category RPMonsters
	Health 1000
	Radius 30
	Height 56
	Mass 1000
	Speed 20
	PainChance 160
	MONSTER
	+FLOORCLIP
	+NOINFIGHTING
	+NOBLOOD
	+DONTHARMCLASS
	+MISSILEMORE
	+MISSILEEVENMORE
	+NORADIUSDMG
	SeeSound "chook/sight"
	ActiveSound "chook/sight"
	PainSound "chook/pain"
	DeathSound "aqua/kill1"
	Obituary "%o chooked out thanks to a Great Fractal Hen."
	Tag "Fractal Hen"
	DropItem "Eggammo"
	DropItem "Eggammo", 224
	DropItem "Eggammo", 192
	DropItem "Eggammo", 160
	DropItem "Eggammo", 128
	States
	{
		Spawn:
			CHKB A 6 A_Look
			Loop
		See:
			CHKB C 5
			CHKB DDEE 3 A_Chase
			CHKB CA 4
			Loop
		Missile:
			CHKB A 0 A_DynamicMusic
			CHKB AB 5 A_FaceTarget
			CHKB B 0 A_Playsound("chook/sight",1)
			CHKB F 8 A_Throwgrenade("EvilEggshot",2,random(8,12),random(6,12),0)
			CHKB A 5
			CHKB B 0 A_Playsound("chook/sight",1)
			CHKB F 8 A_Throwgrenade("EvilEggshot",2,random(8,12),random(6,12),0)
			CHKB A 5
			Goto See
		Pain:
			CHKB C 6 A_Pain
			CHKB A 3 A_Jump(192,2)
			CHKB B 3 A_ThrowGrenade("BigChookGrenade",2,random(2,6),random(6,10),0)
			CHKB AA 1 
			Goto See
		Death.Suck:
			CHKB C 5 A_PlaySound("VacDed",CHAN_BODY)
			CHKB C 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,12)
			Stop
		Death:
			CHKB C 0 A_PlaySound("weapons/cherryboom")
			CHKB C 3 Bright A_SpawnItemEX("BossBoom",random(20,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION) 
			CHKB C 0 A_PlaySound("weapons/cherryboom")
			CHKB C 3 Bright A_SpawnItemEX("BossBoom",random(20,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION) 
			CHKB C 0 A_PlaySound("weapons/cherryboom")
			CHKB C 3 Bright A_SpawnItemEX("BossBoom",random(20,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION) 
			CHKB C 0 A_PlaySound("weapons/cherryboom")
			CHKB C 3 Bright A_SpawnItemEX("BossBoom",random(20,64),random(-64,64),random(-64,64),0,0,0,0,SXF_NOCHECKPOSITION) 
			CHKB C 0 A_PlaySound("weapons/cherryboom")
			DANS R 1 bright A_ThrowGrenade("BigChookGrenade",2,random(2,6),random(6,10),0)
			DANS R 1 bright A_ThrowGrenade("BigChookGrenade",2,random(2,6),random(6,10),0)
			DANS R 1 bright A_ThrowGrenade("BigChookGrenade",2,random(2,6),random(6,10),0)
			DANS S 3 bright A_NoBlocking
			DANS TUVWXYZ 2 bright
			Stop
	}
}

ACTOR BigChookGrenade : EEProjectile
{
	Radius 3
	Height 3
	Speed 10
	Damage 1
	Scale 0.5
	PROJECTILE
	-NOGRAVITY
	RENDERSTYLE Normal
	States
	{
		Spawn:
			CHKS D 3
			loop
		Death:
			CHKS DEDE 2
			CHKS E 0 A_SpawnItemEX("FractalChicken",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}
