// DECORATE FILE IS CURRENTLY EMPTY NOW :V
// This file will remain in case new DECORATE code to be converted has to be hooked up to the mod. >.>
// Diabloist Flare
actor MFlareFX
{   
	Radius 0
	Height 1
	Speed 0
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.67
	States
	{
		Spawn:
			FDFX ABCDEF 4 Bright
			Stop 
	}
}

// Diabloist Gravity Glitter

actor DiabloistHPGlitter : TeleGlitterGenerator1
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("TeleGlitter1", random[TeleGlitter](0,31)-16,random[TeleGlitter](0,31)-16,0, 0,0,0.25, SXF_NOCHECKPOSITION,0)
			TNT1 A 0 A_SpawnItemEx("TeleGlitter1", random[TeleGlitter](0,31)-16,random[TeleGlitter](0,31)-16,0, 0,0,0.25, SXF_NOCHECKPOSITION,0)
			Stop
	}
}
actor DiabloistManaGlitter : TeleGlitterGenerator2
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("TeleGlitter2", random[TeleGlitter](0,31)-16,random[TeleGlitter](0,31)-16,0, 0,0,0.25, SXF_NOCHECKPOSITION,0)
			TNT1 A 0 A_SpawnItemEx("TeleGlitter2", random[TeleGlitter](0,31)-16,random[TeleGlitter](0,31)-16,0, 0,0,0.25, SXF_NOCHECKPOSITION,0)
			Stop
	}
}

// Diabloist Missile Trail
actor DMissileTrail : CFlameFloor { }

actor Diabloist : EECreature
{
	//$Angled
	//$Color 12
	Tag "Diabloist"
	Health 1400
	Radius 20 
	Height 56 
	Mass 750 
	Speed 22.5
	BloodColor "08 08 08"
	Painchance 10
	Painchance "Stomp", 100 // Archviles are hyper suspectible to Stomp attacks [10x pain chance]
	Painchance "Trample", 100 // Archviles are hyper suspectible to Stomp attacks [10x pain chance]
	Painchance "Fist", 100 // Archvile are super suspectible to Punch attacks [10x pain chance]
	Painchance "Kick", 255 // Archviles are hyper suspectible to Kick attack [50x pain chance]
	Painchance "Gauntlets", 100 // Archvile are super suspectible to Punch attacks [10x pain chance]
	Painchance "Chainsaw", 100 // Archvile are super suspectible to Chainsaw attacks [10x pain chance]
	Painchance "Melee", 100 // Archvile are super suspectible to Melee attacks [10x pain chance]
	PainChance "Fire", 0
	PainChance "Ice", 35
	DamageFactor 0.875
	RadiusDamageFactor 0.875
	DamageFactor "Explosive", 0.875
	Damagefactor "Stomp", 5
	Damagefactor "Trample", 5
	DamageFactor "Fist", 5
	DamageFactor "Kick", 5
	DamageFactor "Gauntlets", 5
	DamageFactor "PoisonNeedle", 5
	DamageFactor "Sceptre", 5
	DamageFactor "Chainsaw", 5
	DamageFactor "Melee", 5
	DamageFactor "Crush", 0.8 // 6.4
	DamageFactor "Fire", 0.02
	DamageFactor "Ice", 1.66
	DamageFactor "Holy", 2 // Demons = +200% Holy Damage
	Painchance "Holy", 30 // Demons = +300% Pain Chance
	DamageFactor "Magic", 0.48
	PainChance "Magic", 4
	Species "Diabloist"
	MONSTER
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	+NODROPOFF
	+NORADIUSDMG
	+MISSILEMORE
	+DONTHURTSPECIES
	+AVOIDMELEE
	SeeSound "monster/diasit" 
	PainSound "monster/diapai" 
	DeathSound "monster/diadth" 
	ActiveSound "monster/diaact" 
	Obituary "%o got was set ablaze by a Diabloist."

	States 
	{
		Missile:
			TNT1 A 0
			DIAB G 0 A_Jump(85, "GravityAttackCheck")
		// Archvile-Type Fire Spell
		Missile1:
			DIAB G 0 BRIGHT 
			{
				if (random(1,2) <= 1)
				{
					if (random(1,3) <= 1) { return state("Missile2"); }
					else if (random(1,2) <= 1) { return state("Missile3"); }
					else { return state("Missile4"); }
					return state("");
				}
				return state("");
			}
			DIAB G 0 BRIGHT A_FaceTarget
			DIAB GH 3 Bright 
			{
				A_FaceTarget;
			}
			DIAB I 3 BRIGHT 
			{
				A_VileTarget("DFire");
			}
			DIAB HGHIHGHIHGHIGH 3 BRIGHT 
			{
				A_FaceTarget;
			}
			DIAB I 3 BRIGHT
			{
				A_FaceTarget;
				A_VileAttack("vile/stop",40,140,140,1.5,"Fire",VAF_DMGTYPEAPPLYTODIRECT);
			}
			DIAB G 0 BRIGHT A_MonsterRefire(8,"See")
			DIAB P 0 BRIGHT A_Jump(96,"Missile1")
			DIAB G 0 BRIGHT A_Jump(128,"Missile3")
			Goto See

		// Rapid Fire Shots
		Missile2:
			DIAB NOPNOPNOPNOPNOP 5 BRIGHT
			{
				A_FaceTarget;
				A_SpawnProjectile("DFlare",40,0,random(-4,4));
			}
			DIAB G 0 BRIGHT A_MonsterRefire(8,"See")
			DIAB P 0 BRIGHT A_Jump(96,"Missile2")
			DIAB P 0 BRIGHT A_Jump(64,"Missile3")
			DIAB P 0 BRIGHT A_Jump(128,"Missile4")
			Goto See

		// Firestorm-Equse Attack
		Missile3:
			DIAB GHIJKLMN 5 BRIGHT 
			{
				A_FaceTarget;
			}
			DIAB O 5 BRIGHT 
			{
				A_Playsound ("weapons/firbfi");
				A_FaceTarget;
				A_SpawnProjectile("DMissile",32,0,0);
			}
			DIAB P 5 BRIGHT
			DIAB G 0 BRIGHT A_MonsterRefire(8,"See")
			DIAB P 0 BRIGHT A_Jump(96,"Missile3")
			DIAB P 0 BRIGHT A_Jump(128,"Missile4")
			Goto See

		// Twin Flame Snakes
		Missile4:
			DIAB "[\" 8 BRIGHT
			{
				A_FaceTarget;
			}
			DIAB ] 8 BRIGHT
			{
				A_FaceTarget;
				A_SpawnProjectile("DTracer",0,-40,0);
				A_SpawnProjectile("DTracer",0,40,0);
			}
			DIAB G 0 BRIGHT A_MonsterRefire(8,"See")
			DIAB P 0 BRIGHT A_Jump(96,"Missile4")
			DIAB G 0 BRIGHT A_Jump(8,"Missile1")
			Goto See 

		GravityAttackCheck:
			DIAB G 0 BRIGHT A_DiabloistGravityCheck
		Missile5:
			DIAG A 0 BRIGHT A_FaceTarget
			DIAG A 3 BRIGHT
			{
				A_DiabloistSpawnGravityNodes;
				A_FaceTarget;
			}
			DIAG BCBABCBA 3 Bright // 24/16/8
			{

				A_DiabloistGravityPulse(3.0);
				A_FaceTarget;
			}
			DIAG BCBABCBA 3 Bright // 24/16/8
			{
				A_DiabloistGravityPulse(5.25);
				A_FaceTarget;
			}
			DIAG BCBA 4 Bright // 16/12/8/4
			{
				A_DiabloistGravityPulse(9.75);
				A_FaceTarget;
			}
			DIAG BCBA 4 Bright // 16/12/8/4
			{
				A_DiabloistGravityPulse(12.00);
				A_FaceTarget;
			}
			DIAG DEF 4 Bright // 9/6/3
			{
				A_FaceTarget;
			}
			DIAG G 4 BRIGHT
			{
				A_DiabloistGravity(user_gravitytype);
			}
			DIAG FE 3 Bright // 9/6/3
			{
				A_FaceTarget;
			}
			DIAG A 0 BRIGHT A_Jump(1,"Missile")
			Goto See

		Spawn:
			DIAB A 0 NoDelay 
			DIAB AB 10 A_Look 
			goto Spawn+1
		See:
			DIAB A 0 
			{
				if (health <= ((SpawnHealth() + Stamina) * 0.375)) 
				{
					if (health <= ((SpawnHealth() + Stamina) * 0.125)) 
					{
						A_SetInventory("Speed18PercentBonus",1);
						A_SetInventory("Speed12PercentBonus",0);
					}
					else
					{
						A_SetInventory("Speed12PercentBonus",1);
						A_SetInventory("Speed18PercentBonus",0);
					}
				}
				else
				{
					A_SetInventory("Speed18PercentBonus",0);
					A_SetInventory("Speed12PercentBonus",0);
				}
			}
			DIAB AABBCCDDEEFF 2 
			{
				if (random (1,256) <= 160) 
				{
					A_Chase("","Missile", CHF_FASTCHASE | CHF_RESURRECT);
				}
				else
				{
					A_Chase("","Missile", CHF_RESURRECT); 
				} 
			}
			loop

		Heal:
			DIAB "[" 10 BRIGHT
			{
				A_DiabHealProj(-24.0,-40.0);
			}
			DIAB "\" 10 BRIGHT
			{
				A_DiabHealProj(0.0,0.0);
			}
			DIAB "]" 10 BRIGHT
			{
				A_DiabHealProj(24.0,40.0);
			}
			Goto See

		Pain: 
			DIAB Q 5 
			DIAB Q 5 A_Pain 
			DIAB G 0 A_Jump(112, "GravityAttackCheck")
			DIAB G 0 A_Jump(120, "Missile1")
			Goto See 
		Pain.Fist:
			DIAB Q 5
			DIAB Q 5 A_Pain 
			DIAB QQ 5
			DIAB G 0 A_Jump(112, "GravityAttackCheck")
			DIAB G 0 A_Jump(120, "Missile1")
			Goto See
		Pain.Kick:
			DIAB Q 5
			DIAB Q 5 A_Pain 
			DIAB QQQQ 5
			DIAB G 0 A_Jump(112, "GravityAttackCheck")
			DIAB G 0 A_Jump(120, "Missile1")
			Goto See
		Pain.Ice:
			DIAB Q 5   
			DIAB Q 5 A_Pain 
			DIAB Q 10
			DIAB Q 0 A_Jump(128, 4)
			DIAB Q 5
			DIAB Q 0 A_Jump(128, 2)
			DIAB Q 5
			DIAB Q 0
			Goto See
		Death:
			DIAB Q 7 
			DIAB R 7 A_Scream 
			DIAB S 7 A_NoBlocking 
			DIAB TUVW 7 
			DIAB XY 5 
			TNT1 A 0 
			DIAB Z -1 
			Stop    
		Ice:
			DIAB Q 5 A_GenericFreezeDeath
			DIAB Q 1 A_FreezeDeathChunks
			wait
		/*
		Raise:
			DIAB Y 5 
			DIAB X 5
			DIAB WVUTSRQ 7
			goto See
		*/
	}
}

actor DMissile : CFlameMissileNew
{
	Tag "Diabloist Fireblast"
	DamageType "Fire"
	Damage (8 * random(1,8))
	+THRUSPECIES
	Species "Diabloist"
	States
	{
		Spawn:
			CFFX A 0
			"####" AAAA 1 Bright
			"####" A 1 A_CFlamePuff
			Goto Death+1
		Death:
			"####" A 1 Bright A_CFlameMissile("DMissileCircle")
			"####" ABC 3 Bright
			"####" D 4 Bright
			"####" E 3 Bright
			"####" F 4 Bright
			"####" G 3 Bright
			"####" H 4 Bright
			"####" I 3 Bright
			"####" J 4 Bright
			"####" K 3 Bright
			"####" L 4 Bright
			"####" M 3 Bright
			Stop
	}
}

actor DTracer : EEProjectile
{
	Tag "Diabloist Tracer"
	Radius 5
	Height 5
	Speed 15
	ReactionTime 175
	Damage (10 * random(1,8))
	DamageType "Fire"
	RenderStyle ADD
	Alpha 0.67
	PROJECTILE
	+SEEKERMISSILE
	+FLOORHUGGER
	-NOGRAVITY
	Obituary "%o got was set ablaze by a Diabloist."
	Seesound "weapons/diasht"
	DeathSound "weapons/firex3"   
	+THRUSPECIES
	Species "Diabloist"
	
	States
	{
		Spawn:
			TNT1 A 1 Bright A_SeekerMissile (10,15)
			TNT1 A 0 Bright A_Countdown
			TNT1 A 0 Bright A_CustomMissile("DTracerPuff",0,0,0,0)
			loop
		Death:
			FTRA K 4 Bright
			FTRA L 4 Bright A_Explode(64,64,0)
			FTRA MNO 3 Bright
			stop
	}
}

actor DTracerPuff : EEProjectile
{
	Tag "Diabloist Tracer"
	Radius 1
	Height 1
	Speed 0
	RENDERSTYLE ADD
	DamageType "Fire"
	ALPHA 0.67
	PROJECTILE
	+FLOORHUGGER
	-NOGRAVITY
	+THRUSPECIES
	Species "Diabloist"
	States
	{
		Spawn:
			FTRA A 0
			"####" A 1 Bright A_Explode(4,8,0)
			"####" A 1 Bright
			"####" A 1 Bright
			"####" B 1 Bright A_Explode(4,8,0)
			"####" B 1 Bright
			"####" B 1 Bright
			"####" C 1 Bright A_Explode(4,8,0)
			"####" C 1 Bright
			"####" C 1 Bright
			"####" D 1 Bright A_Explode(4,8,0)
			"####" D 1 Bright
			"####" D 1 Bright
			"####" E 1 Bright A_Explode(4,8,0)
			"####" E 1 Bright
			"####" E 1 Bright
			"####" F 1 Bright A_Explode(4,8,0)
			"####" F 1 Bright
			"####" F 1 Bright
			"####" G 1 Bright A_Explode(4,8,0)
			"####" G 1 Bright
			"####" G 1 Bright
			"####" H 1 Bright A_Explode(4,8,0)
			"####" H 1 Bright
			"####" H 1 Bright
			"####" I 1 Bright A_Explode(4,8,0)
			"####" I 1 Bright
			"####" I 1 Bright
			"####" J 1 Bright A_Explode(4,8,0)
			"####" J 1 Bright
			"####" J 1 Bright
			stop
	}
}

actor DMissileCircle : CircleFlameNew 
{ 
	Tag "Diabloist Fireblast"
	+THRUSPECIES
	Species "Diabloist"
}

Actor DFire : EEProjectile
{
	Tag "Diabloic FireHex"
	Obituary "%o got was set ablaze by a Diabloist."
	Radius 0
	Height 1
	Speed 0
	RENDERSTYLE ADD
	DamageType "Fire"
	ALPHA 1.00
	+NOGRAVITY
	+SEEKERMISSILE
	+NOTARGET
	+NODAMAGETHRUST
	+THRUSPECIES
	Species "Diabloist"
	States
	{
		Spawn:
			HLFR A 2 Bright
			{
				A_StartFire;
				A_Explode(4,32,0);
			}
			HLFR BABCB 2 Bright
			{
				A_Fire;
				A_Explode(4,32,0);
			}
			HLFR CBCDCDCDEDED 2 Bright
			{
				A_Fire;
				A_Explode(5,32,0);
			}
			HLFR E 2 Bright
			{
				A_FireCrackle;
				A_Explode(4,32,0);
			}
			HLFR FEF 2 Bright
			{
				A_Fire;
				A_Explode(3,32,0);
			}
			HLFR EFG 2 Bright
			{
				A_Fire;
				A_Explode(2,32,0);
			}
			HLFR HGHGH 2 Bright
			{
				A_Fire;
				A_Explode(1,32,0);
			}
			stop
	}
}

actor DFlare : EEProjectile
{
	Tag "Diabloist Flare"
	Radius 5
	Height 5
	Speed 25
	Damage (4 * random(1,8))
	RENDERSTYLE ADD
	DamageType "Fire"
	ALPHA 0.85
	PROJECTILE
	Obituary "%o got was set ablaze by a Diabloist."
	Seesound "weapons/firmfi"
	DeathSound "weapons/firex4"
	+THRUSPECIES
	Species "Diabloist"
	States
	{
		Spawn:
			VBAL A 0
			"####" A 1 Bright A_CustomMissile("MFlareFX",0,0,0,0)
			"####" AA 1 Bright
			"####" B 1 Bright A_CustomMissile("MFlareFX",0,0,0,0)
			"####" BB 1 Bright
			loop
		Death:
			CBAL CDEFG 3 Bright
			stop
	}
}

actor DiabloistGravityHPTargeter : EEProjectile
{
	Tag "Diabloist Gravity Spell"
	var int user_timer;
	var int user_timer_times;
	var int user_timer_timesmax;
	RenderStyle None
	Alpha 0.00
	PROJECTILE
	+SEEKERMISSILE
	States
	{
		Spawn:
			TGLT A 0 NoDelay { user_timer_timesmax = 36; }
			goto Idle
		Idle:
			TGLT AB 1
			{
				A_Fire(0);
				
				user_timer += 1;
				if (user_timer >= 2) 
				{
					A_SpawnItemEx("DiabloistHPGlitter", random(-32,32),random(-32,32),random(-16,16), 0,0,0, 0,SXF_NOCHECKPOSITION,0);
					user_timer = 0; 
					user_timer_times += 1; 
				}
				if (user_timer_times >= user_timer_timesmax) 
				{
					A_FadeOut(1.0,1);
				}
			}
			loop
	}
}

actor DiabloistGravityHPTargeter2 : DiabloistGravityHPTargeter
{
	States
	{
		Spawn:
			TGLT A 0 NoDelay { user_timer_timesmax = 24; }
			goto Idle
	}
}

actor DiabloistGravityHPTargeter3 : DiabloistGravityHPTargeter
{
	States
	{
		Spawn:
			TGLT A 0 NoDelay { user_timer_timesmax = 12; }
			goto Idle
	}
}

actor DiabloistGravityManaTargeter : EEProjectile
{
	Tag "Diabloist Gravity Spell"
	var int user_timer;
	var int user_timer_times;
	var int user_timer_timesmax;
	RenderStyle None
	Alpha 0.00
	PROJECTILE
	+SEEKERMISSILE
	States
	{
		Spawn:
			TGLT F 0 NoDelay { user_timer_timesmax = 36; }
			goto Idle
		Idle:
			TGLT FG 1
			{
				A_Fire(0);
				
				user_timer += 1;
				if (user_timer >= 2) 
				{
					A_SpawnItemEx("DiabloistManaGlitter", random(-32,32),random(-32,32),random(-16,16), 0,0,0, 0,SXF_NOCHECKPOSITION,0);
					user_timer_times += 1; 
				}
				if (user_timer_times >= user_timer_timesmax) 
				{
					A_FadeOut(1.0,1);
				}
			}
			loop
	}
}

actor DiabloistGravityManaTargeter2 : DiabloistGravityManaTargeter
{
	States
	{
		Spawn:
			TGLT A 0 NoDelay { user_timer_timesmax = 24; }
			goto Idle
	}
}

actor DiabloistGravityManaTargeter3 : DiabloistGravityManaTargeter
{
	States
	{
		Spawn:
			TGLT A 0 NoDelay { user_timer_timesmax = 12; }
			goto Idle
	}
}

actor ReviveProj : EEProjectile
{
	//+FLOAT
	//+NOBLOCKMONST
	//+NOGRAVITY
	+FRIENDLY
	+NOCLIP
	Radius 50
	Height 5
	PROJECTILE
	RENDERSTYLE NONE
	Speed 7
	States
	{
		Spawn:
			TNT1 AAAAA 6 
			{
				A_Chase("","",CHF_RESURRECT);
			}
			Stop
		Death:
			TNT1 A 1
			Stop
		Heal:
			TNT1 A 1
			Stop
	}
}

// Revive Projectiles [used by monsters]
actor ReviveProj2 : ReviveProj
{
	//-FRIENDLY
}
