// DECORATE FILE IS CURRENTLY EMPTY NOW :V
// This file will remain in case new DECORATE code to be converted has to be hooked up to the mod. >.>
//CREME DE BRATTE
actor CremeDeBratte : DDOrigCreature 
{
		//$Category RPMonsters
		Health 150
		Radius 20
		Height 56
		Mass 1000
		Speed 20
		PainChance 256
		DamageFactor "Cricket", 2
		DamageFactor "Chop", 2
		DamageFactor "Fire", 2
		DamageFactor "CherryBomb", 2
		MONSTER
		+FLOORCLIP
		+NOINFIGHTING
		+NOTARGET
		+NOBLOOD
		+DONTHARMCLASS
		+MISSILEMORE
		+MISSILEEVENMORE
		PainSound "tart/pain"
		DeathSound "aqua/kill1"
		Obituary "%o was creamed by a Creme de Bratte."
		Tag "Creme De Bratte"
		Species "DeBratte"
	
	
	States
	{
		Spawn:
			TART AB 10 A_Look
			Loop
		See:
			TART AB 5
			TART C 0 A_StartSound("tart/hop",CHAN_BODY)
			TART CCDD 3 A_EEChase("Melee","Missile",0,1,3,1,1)
			TART EAE 2
			TART A 2 
			Loop
		Missile:
			TART H 0 A_DynamicMusic
			TART H 0 A_StartSound("aqua/kill2",1)
			TART HIJ 5
			TART K 12 A_CustomMissile("SeedBall",40,0,0)
			TART H 3
			Goto See
		Pain:
			TART F 3 A_EEPain
			TART GFG 3 
			Goto See
		Pain.Fire:
			BART F 3 A_EEPain
			BART G 3 A_Die("Fire")
			Goto Death.Fire
		Pain.Cherrybomb:
			BART F 3 A_EEPain
			BART G 3 A_Die("Cherrybomb")
			Goto Death.Fire
		Death.Suck:
			TART F 5 A_StartSound("VacDed",CHAN_BODY)
			TART F 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,4)
			Stop
		Death:
			TART F 3 A_EEPain
			TART GFG 3 
			DANS R 5 bright A_Scream
			DANS S 3 bright A_NoBlocking
			DANS TUVWXYZ 2 bright
			Stop
		Death.Fire:
		Death.Cherrybomb:
			BART F 3 bright
			BART G 0 A_SpawnItemEx("CremeDeBratte_Torched",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

actor CremeDeBratte_Torched : DDOrigCreature
{
		//$Category RPMonsters
		Health 150
		Radius 20
		Height 56
		Mass 1000
		Speed 20
		PainChance 256
		DamageFactor "Fire", 0.5
		DamageFactor "CherryBomb", 0.5
		MONSTER
		+FLOORCLIP
		+NOINFIGHTING
		+NOBLOOD
		+DONTHARMCLASS
		+MISSILEMORE
		+MISSILEEVENMORE
		PainSound "tart/pain"
		DeathSound "aqua/kill1"
		Obituary "%o was creamed by a Torched Creme de Bratte."
		Tag "Torched Creme De Bratte"
		Species "DeBratte"
	
	
	States
	{
		Spawn:
			BART AB 10 bright A_Look
			Loop
		See:
			BART AB 4 bright
			BART C 0 bright A_StartSound("tart/hop",CHAN_BODY)
			BART CCDD 3 bright A_EEChase("Melee","Missile",0,1,3,1,1)
			BART EAE 2 bright
			BART A 2 bright 
			Loop
		Missile:
			BART B 0 bright A_DynamicMusic
			BART B 0 bright A_StartSound("aqua/kill2",1)
			BART BE 5 bright
			BART E 12 bright A_CustomMissile("StarBall",40,0,0)
			BART A 3 bright
			Goto See
		Pain:
			BART F 3 bright A_EEPain
			BART GFG 3 bright 
			Goto See
		Death.Suck:
			BART F 5 bright A_StartSound("VacDed",CHAN_BODY)
			BART F 2 bright A_RadiusGive("VacAmmo",128,RGF_PLAYERS,4)
			Stop
		Death:
			BART F 3 bright A_EEPain
			BART GFG 3 bright 
			BMX2 A 6 Bright A_StartSound("weapons/cherryboom")
			BMX2 B 0 A_AlertMonsters
			BMX2 B 6 Bright A_Explode
			BMX2 CDEFG 5 Bright
			Stop
	}
}


//ANGEL (PUSHY)
actor PushyAngel : DDOrigCreature
{
		//$Category RPMonsters
		Health 350
		Radius 20
		Height 80
		Speed 10
		PainChance 256
		Mass 1500
		DamageFactor "Fire", 2
		DamageFactor "Chop", 2
		DamageFactor "Holy", 0.125
		DamageFactor "Zap", 5
		MONSTER 
		+FLOORCLIP
		+NOINFIGHTING
		+NOTARGET
		+NOBLOOD
		+FLOAT
		+NOGRAVITY
		+FLOATBOB
		+DONTFALL
		SeeSound "angel/active"
		ActiveSound "angel/active"
		DeathSound "virtuoso/death"
		Obituary "%o was punished by a pushy angel."
		Tag "Angel (Pushy)"
		DropItem "MusicAmmo" 96
		DropItem "MusicAmmo" 24
		DropItem "MusicAmmo" 6
		DropItem "AppleWine" 96
		DropItem "AppleWine" 6
		
	
	States
	{
		Spawn:
			ANGL C 0 A_SpawnItemEx("Mininote", 0, 0, 25, 0, random(-1, 1), random(-1, 1))
			ANGL ABC 8 A_Look
			Loop
		See:
			ANGL AABBCC 3 A_EEChase("Melee","Missile",0,1,3,1,1)
			ANGL C 0 A_SpawnItemEx("Mininote", 0, 0, 25, 0, random(-1, 1), random(-1, 1))
			Loop
		Missile:
			ANGA A 0 A_DynamicMusic
			ANGA A 0 A_Jump(128,"DeathSong")
			ANGA A 0 A_Jump(160,"AnnoyingSong")
			ANGA A 0 A_StartSound("Handmother/Zap")
			ANGA AB 5 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA B 3 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA B 3 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA C 5 bright
			ANGL A 3
			Goto See
		AnnoyingSong:
			ANGL A 0 A_StartSound("angel/ballattack",CHAN_VOICE)
			ANGL A 8 A_Facetarget
			ANGA D 8 A_StartSound("virtuoso/musicfire")
			ANGA D 6 A_SpawnProjectile("MusicBall",48)
			ANGA D 10
			ANGL BA 4
			Goto See
		DeathSong:
			ANGL A 0 A_StartSound("angel/deathsong",CHAN_VOICE)
			ANGL A 8 A_Facetarget
			ANGA D 8
			ANGA DDDDDD 6 A_SpawnProjectile("JorgSong",48)
			ANGA D 10
			ANGL BA 4
			Goto See
		Pain:
			ANGP A 3 A_EEPain
			ANGP A 6 //A_DynamicMusic
			ANGA A 6
			Goto See
		Death.Suck:
			ANGP A 5 A_StartSound("VacDed",CHAN_BODY)
			ANGP A 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,12)
			Stop
		Death:
			ANGL D 6 bright A_Scream
			ANGL EF 5 bright 
			ANGL G 4 bright A_NoBlocking
			Stop
	}
}

//BIG HULKING DEVIL
actor Devil : DDOrigCreature
{
		//$Category RPMonsters
		Health 450
		Radius 32
		Height 80
		Speed 18
		PainChance 200
		Painchance "Suck", 64
		DamageFactor "Zap", 5
		MeleeThreshold 208
		MeleeRange 80
		MaxTargetRange 512
		Mass 5000
		Scale 1.5
		MONSTER 
		+FLOORCLIP
		+NOBLOOD
		+NOINFIGHTING
		+DONTHARMCLASS
		SeeSound "muddy/sight"
		PainSound "muddy/pain"
		DeathSound "virtuoso/death"
		ActiveSound "muddy/active"
		MeleeSound "mallethit"
		Obituary "%o was flattened by a big hulking devil."
		Tag "Big Hulking Devil"
		MissileType "BadJackshot"
		DropItem "JackAmmo"
	
	
	States
	{
		Spawn:
			ONII A 10 A_Look
			Loop
		See:
			ONII AABBCCDD 3 A_EEChase("Melee","Missile",0,1,3,1,1)
			Loop
		Melee:
			ONII E 0 A_DynamicMusic
			ONII EF 4 A_FaceTarget
			ONII G 6 A_CustomMeleeAttack (random(35,50), "mallethit", "malletswing", "bonk", 1) 
			ONII HIJ 6 
			Goto See
		Missile:
			ONII J 0 A_StartSound("muddy/sight",CHAN_BODY)
			ONII J 0 A_DynamicMusic
			ONII JI 6 A_FaceTarget
			ONII F 7 A_ComboAttack
			ONII J 6
			Goto See
		Pain:
			ONII E 6 A_EEPain
			ONII J 3 //A_DynamicMusic
			Goto See
		Death.Suck:
			ONII E 5 A_StartSound("VacDed",CHAN_BODY)
			ONII E 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,12)
			Stop
		Death:
			ONII E 8 A_StartSound("slots/death",1)
			ONII K 8 bright A_Scream
			DANS R 5 bright A_NoBlocking
			DANS STUVWXYZ 5 bright
			Stop
	}
}

#include "decorate/sekrit.dec"