// DECORATE FILE IS CURRENTLY EMPTY NOW :V
// This file will remain in case new DECORATE code to be converted has to be hooked up to the mod. >.>
Actor GhostCloudSpawner : EEBaseZSC
{
		radius 0
		height 0
		+NOGRAVITY
		+NOBLOCKMAP
		+NOLIFTDROP
		+NOTARGET
		+DONTSQUASH
		+NOTELEOTHER
		+NONSHOOTABLE
	
	
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_SpawnItemEx("GhostCloud", 0, 0, 0, random(-2, 2), random(-2, 2), random(-1, 1), 0.0, SXF_TRANSFERPOINTERS)
			Stop
	}
}

Actor GhostCloud : EEBaseZSC
{
	
		renderstyle translucent
		alpha 0.9
		Scale 0.5
		radius 0
		height 0
		+NOGRAVITY
		+NOBLOCKMAP
		+NOLIFTDROP
		+NOTARGET
		+DONTSQUASH
		+NOTELEOTHER
		+NONSHOOTABLE
		+DONTSPLASH
	
	
	States
	{
		Spawn:
			VAM6 A 0
			VAM6 A 0 A_Jump(128, 6)
			VAM6 ABCDE 3 A_FadeOut(0.05)
			Goto Spawn+1
			VAM6 F 0 A_Jump(128, 6)
			VAM6 FGHIJ 3 A_FadeOut(0.05)
			Goto Spawn+7
			VAM6 KLMNO 3 A_FadeOut(0.05)
			Goto Spawn+12
	}
}

//FORSAKEN
actor Forsaken : CoSForsaken
{
	
		//$Category RPMonsters
		Health 100
		Radius 20
		Height 56
		Speed 12
		Scale 1
		Mass 200
		PainChance 100
		DamageFactor "Zap", 5
		DamageFactor "Lead", 0.5
		MONSTER 
		+FLOORCLIP
		+NOBLOOD
		+NOINFIGHTING
		+NOTARGET
		+FLOAT
		+NOGRAVITY
		+DONTFALL
		+STEALTH
		Renderstyle "Add"
		Alpha 0.75
		SeeSound "forsaken/sight"
		PainSound "forsaken/pain"
		DeathSound "forsaken/pain"
		ActiveSound "forsaken/active"
		Obituary "%o became one of the Forsaken."
		Tag "Forsaken"
	
	
	States
	{
		Spawn:
			TNT1 A 6 A_Look
			Loop
		See:
			GOST AABBCC 3 A_EEChase("Melee","Missile",0,1,3,1,1)
			TNT1 A 0 
			{
				A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
				if (random(1,16) <= 13)
				{
					A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
					if (random(1,16) <= 9)
					{
						A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
					}
				}
			}
			GOST DDEEFF 3 A_EEChase("Melee","Missile",0,1,3,1,1)
			TNT1 AAA 0
			{
				A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
				if (random(1,16) <= random(12,14))
				{
					A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
					if (random(1,16) <= random(8,10))
					{
						A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, SXF_CLIENTSIDE|SXF_SETTRACER);
					}
				}
			}
			Loop
		Melee:
			GOST G 0 
			{
				A_DynamicMusic;
			}
			GOST GH 6 A_FaceTarget
			GOST I 6 A_CustomMeleeAttack(random(15,35), "forsaken/bite", "forsaken/bite", "", 1) 
			GOST I 6 
			GOST C 6 
			Goto See
		Pain:
			GOST K 6 A_EEPain
			TNT1 AA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			GOST G 3 
			Goto See
		Death.Suck:
			GOST K 5 A_StartSound("VacDed",CHAN_BODY)
			GOST K 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,3)
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			Stop
		Death:
			GOST J 5 A_Scream
			GOST KLM 4 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			GOST N 5 A_NoBlocking
			Stop
	}
}

actor Forsaken_Jorgensen : CoSForsaken
{
		//$Category RPMonsters
		Health 300
		Radius 20
		Height 56
		Speed 12
		Scale 1
		Mass 200
		PainChance 200
		DamageFactor "Zap", 5
		DamageFactor "Lead", 0.5
		DamageFactor "Suck", 0
		MONSTER 
		+FLOORCLIP
		+NOBLOOD
		+NOINFIGHTING
		+NOTARGET
		+FLOAT
		+NOGRAVITY
		+DONTFALL
		+GHOST
		+STEALTH
		Renderstyle "Add"
		Alpha 0.75
		SeeSound "jorg/sight"
		PainSound "jorg/pain"
		DeathSound "jorg/death"
		ActiveSound "jorg/active"
		Obituary "%o became one of the Forsaken."
		Tag "Forsaken Jorgensen"
	
	
	States
	{
		Spawn:
			TNT1 A 6 A_Look
			Loop
		See:
			GORG AABBCC 2 A_Chase
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			GORG DDEEFF 2 A_Chase
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			Loop
		Melee:
			GORG G 0 A_DynamicMusic
			GORG GH 6 A_FaceTarget
			GORG I 6 A_CustomMeleeAttack (random(15,35), "ghost/bite", "ghost/bite", "", 1) 
			GORG I 6 
			GORG C 6 
			Goto See
		Pain:
			GORG J 6 A_Pain
			TNT1 AA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			GORG G 3 
			Goto See
		Death.Suck:
			GORG J 5 A_PlaySound("VacDed",CHAN_BODY)
			GORG J 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,3)
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			Stop
		Death:
			GORG J 5 A_Scream
			GOST KLM 4 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			GOST N 5 A_NoBlocking
			Stop
	}
}


actor Forsaken_Quisling : CoSForsaken
{
		//$Category RPMonsters
		Health 250
		Radius 20
		Height 56
		Speed 12
		Scale 1
		Mass 200
		PainChance 200
		DamageFactor "Zap", 2.5
		DamageFactor "Lead", 1
		DamageFactor "Suck", 0
		MONSTER 
		+FLOORCLIP
		+NOBLOOD
		+NOINFIGHTING
		+NOTARGET
		+FLOAT
		+NOGRAVITY
		+DONTFALL
		+GHOST
		+STEALTH
		Renderstyle "Add"
		Alpha 0.75
		SeeSound "quisling/sight"
		PainSound "quisling/pain"
		DeathSound "quisling/death"
		ActiveSound "quisling/active"
		Obituary "%o became one of the Forsaken."
		Tag "Forsaken Quisling"
	
	
	States
	{
		Spawn:
			TNT1 A 6 A_Look
			Loop
		See:
			QORG AABBCC 2 A_Chase
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			QORG DDEEFF 2 A_Chase
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			Loop
		Melee:
			QORG G 0 A_DynamicMusic
			QORG GH 6 A_FaceTarget
			QORG I 6 A_CustomMeleeAttack (random(15,35), "ghost/bite", "ghost/bite", "", 1) 
			QORG I 6 
			QORG C 6 
			Goto See
		Pain:
			QORG J 6 A_Pain
			TNT1 AA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			QORG G 3 
			Goto See
		Death.Suck:
			QORG J 5 A_PlaySound("VacDed",CHAN_BODY)
			QORG J 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,3)
			TNT1 AAA 0 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			Stop
		Death:
			QORG J 5 A_Scream
			QOST KLM 4 A_SpawnItemEx("GhostCloudSpawner", 0, 0, 30, 0, 0, 0, 0, 128)
			QOST N 5 A_NoBlocking
			Stop
	}
}

//CREME DE BRATTE
actor CremeDeBratte : DDOrigCreature 
{
		//$Category RPMonsters
		Health 150
		Radius 20
		Height 56
		Mass 1000
		Speed 20
		PainChance 256
		DamageFactor "Cricket", 2
		DamageFactor "Chop", 2
		DamageFactor "Fire", 2
		DamageFactor "CherryBomb", 2
		MONSTER
		+FLOORCLIP
		+NOINFIGHTING
		+NOTARGET
		+NOBLOOD
		+DONTHARMCLASS
		+MISSILEMORE
		+MISSILEEVENMORE
		PainSound "tart/pain"
		DeathSound "aqua/kill1"
		Obituary "%o was creamed by a Creme de Bratte."
		Tag "Creme De Bratte"
		Species "DeBratte"
	
	
	States
	{
		Spawn:
			TART AB 10 A_Look
			Loop
		See:
			TART AB 5
			TART C 0 A_PlaySound("tart/hop",CHAN_BODY)
			TART CCDD 3 A_Chase
			TART EAE 2
			TART A 2 
			Loop
		Missile:
			TART H 0 A_DynamicMusic
			TART H 0 A_Playsound("aqua/kill2",1)
			TART HIJ 5
			TART K 12 A_CustomMissile("SeedBall",40,0,0)
			TART H 3
			Goto See
		Pain:
			TART F 3 A_Pain
			TART GFG 3 
			Goto See
		Pain.Fire:
			BART F 3 A_Pain
			BART G 3 A_Die("Fire")
			Goto Death.Fire
		Pain.Cherrybomb:
			BART F 3 A_Pain
			BART G 3 A_Die("Cherrybomb")
			Goto Death.Fire
		Death.Suck:
			TART F 5 A_PlaySound("VacDed",CHAN_BODY)
			TART F 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,4)
			Stop
		Death:
			TART F 3 A_Pain
			TART GFG 3 
			DANS R 5 bright A_Scream
			DANS S 3 bright A_NoBlocking
			DANS TUVWXYZ 2 bright
			Stop
		Death.Fire:
		Death.Cherrybomb:
			BART F 3 bright
			BART G 0 A_SpawnItemEx("CremeDeBratte_Torched",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Stop
	}
}

actor CremeDeBratte_Torched : DDOrigCreature
{
		//$Category RPMonsters
		Health 150
		Radius 20
		Height 56
		Mass 1000
		Speed 20
		PainChance 256
		DamageFactor "Fire", 0.5
		DamageFactor "CherryBomb", 0.5
		MONSTER
		+FLOORCLIP
		+NOINFIGHTING
		+NOBLOOD
		+DONTHARMCLASS
		+MISSILEMORE
		+MISSILEEVENMORE
		PainSound "tart/pain"
		DeathSound "aqua/kill1"
		Obituary "%o was creamed by a Torched Creme de Bratte."
		Tag "Torched Creme De Bratte"
		Species "DeBratte"
	
	
	States
	{
		Spawn:
			BART AB 10 bright A_Look
			Loop
		See:
			BART AB 4 bright
			BART C 0 bright A_PlaySound("tart/hop",CHAN_BODY)
			BART CCDD 3 bright A_Chase
			BART EAE 2 bright
			BART A 2 bright 
			Loop
		Missile:
			BART B 0 bright A_DynamicMusic
			BART B 0 bright A_Playsound("aqua/kill2",1)
			BART BE 5 bright
			BART E 12 bright A_CustomMissile("StarBall",40,0,0)
			BART A 3 bright
			Goto See
		Pain:
			BART F 3 bright A_Pain
			BART GFG 3 bright 
			Goto See
		Death.Suck:
			BART F 5 bright A_PlaySound("VacDed",CHAN_BODY)
			BART F 2 bright A_RadiusGive("VacAmmo",128,RGF_PLAYERS,4)
			Stop
		Death:
			BART F 3 bright A_Pain
			BART GFG 3 bright 
			BMX2 A 6 Bright A_PlaySound("weapons/cherryboom")
			BMX2 B 0 A_AlertMonsters
			BMX2 B 6 Bright A_Explode
			BMX2 CDEFG 5 Bright
			Stop
	}
}


//ANGEL (PUSHY)
actor PushyAngel : DDOrigCreature
{
		//$Category RPMonsters
		Health 350
		Radius 20
		Height 80
		Speed 10
		PainChance 256
		Mass 1500
		DamageFactor "Fire", 2
		DamageFactor "Chop", 2
		DamageFactor "Holy", 0.125
		DamageFactor "Zap", 5
		MONSTER 
		+FLOORCLIP
		+NOINFIGHTING
		+NOTARGET
		+NOBLOOD
		+FLOAT
		+NOGRAVITY
		+FLOATBOB
		+DONTFALL
		SeeSound "angel/active"
		ActiveSound "angel/active"
		DeathSound "virtuoso/death"
		Obituary "%o was punished by a pushy angel."
		Tag "Angel (Pushy)"
		DropItem "MusicAmmo" 96
		DropItem "MusicAmmo" 24
		DropItem "MusicAmmo" 6
		DropItem "AppleWine" 96
		DropItem "AppleWine" 6
		
	
	States
	{
		Spawn:
			ANGL C 0 A_SpawnItemEx("Mininote", 0, 0, 25, 0, random(-1, 1), random(-1, 1))
			ANGL ABC 8 A_Look
			Loop
		See:
			ANGL AABBCC 3 A_Chase
			ANGL C 0 A_SpawnItemEx("Mininote", 0, 0, 25, 0, random(-1, 1), random(-1, 1))
			Loop
		Missile:
			ANGA A 0 A_DynamicMusic
			ANGA A 0 A_Jump(128,"DeathSong")
			ANGA A 0 A_Jump(160,"AnnoyingSong")
			ANGA A 0 A_PlaySound("Handmother/Zap")
			ANGA AB 5 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA B 3 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA B 3 bright A_Facetarget
			ANGA C 3 bright A_SpawnProjectile("BadZapShot",48)
			ANGA C 5 bright
			ANGL A 3
			Goto See
		AnnoyingSong:
			ANGL A 0 A_Playsound("angel/ballattack",CHAN_VOICE)
			ANGL A 8 A_Facetarget
			ANGA D 8 A_Playsound("virtuoso/musicfire")
			ANGA D 6 A_SpawnProjectile("MusicBall",48)
			ANGA D 10
			ANGL BA 4
			Goto See
		DeathSong:
			ANGL A 0 A_Playsound("angel/deathsong",CHAN_VOICE)
			ANGL A 8 A_Facetarget
			ANGA D 8
			ANGA DDDDDD 6 A_SpawnProjectile("JorgSong",48)
			ANGA D 10
			ANGL BA 4
			Goto See
		Pain:
			ANGP A 3 A_Pain
			ANGP A 6 //A_DynamicMusic
			ANGA A 6
			Goto See
		Death.Suck:
			ANGP A 5 A_PlaySound("VacDed",CHAN_BODY)
			ANGP A 2 A_RadiusGive("VacAmmo",128,RGF_PLAYERS,12)
			Stop
		Death:
			ANGL D 6 bright A_Scream
			ANGL EF 5 bright 
			ANGL G 4 bright A_NoBlocking
			Stop
	}
}

